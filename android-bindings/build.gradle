apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion target_android_version

    defaultConfig {
        //noinspection MinSdkTooLow
        minSdkVersion 12 // View.OnAttachStateChangeListener is since 12
        targetSdkVersion target_android_version
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    lintOptions {
        disable 'GradleCompatible'
    }
}

repositories {
    google()
}

dependencies {
    // these are *.aar deps which can be opened only by Android plug-in. Thus, we're com.android.library
    compileOnly "com.android.support:design:$support_version"
    compileOnly "com.android.support:cardview-v7:$support_version"
    compileOnly "com.android.support:recyclerview-v7:$support_version"
    compileOnly 'androidx.annotation:annotation:1.2.0'
    compileOnly 'com.google.android.material:material:1.0.0'
    compileOnly 'androidx.cardview:cardview:1.0.0'
    compileOnly 'androidx.recyclerview:recyclerview:1.0.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "com.github.Miha-x64.Kotlin-MPP_Collection_utils:Collection-utils-jvm:$collection_utils_version"
    implementation project(':properties') // subscriptions & reactivity
    implementation project(':persistence') // saving/restoring state, tools for wrapping SharedPreferences, JSON, Parcel

    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.squareup.okio:okio:$okio_version"
    testImplementation project(':etc:testing')
    testImplementation project(':extended-persistence') // partial(Struct) is here
    testImplementation 'org.robolectric:robolectric:4.8.1'
    testImplementation project(':sql')
    testImplementation project(':sql:testing')
    testImplementation project(':android-json')
    testImplementation project(':android-json-on-jvm')
}

ext {
    PUBLISH_GROUP_ID = 'su.lychee'
    PUBLISH_ARTIFACT_ID = 'android-bindings'
    PUBLISH_NAME = 'Android Bindings for Lychee'
    PUBLISH_DESCRIPTION = 'Bindings between Lychee Properties and Android Views'
}
apply from: rootProject.file('etc/publish-module.gradle')
